WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

term          = { lam_sequence }
lam_sequence  = { intersection ~ ("->" ~ intersection)* }
intersection  = { application ~ ("&" ~ application)* }
application   = { then_chain ~ (then_chain)* }
then_chain    = { modified_term ~ ("." ~ modified_term)* }
modified_term = { (modifier ~ WHITESPACE)* ~ atomic_term }
atomic_term   = { reflect | record | string | natural | identifier | record_type | "(" ~ term ~ ")" | empty }
empty         = { "()" }
modifier      = { "@" }
reflect       = { "@@" }
universe      = { "*" }

separator = _{ "," | ";" }
key       =  { identifier | string }

record     = { "(" ~ assignment ~ (separator ~ assignment)* ~ separator? ~ ")" }
assignment = { key ~ "=" ~ term }

record_type = { "{" ~ ascription ~ (separator ~ ascription)* ~ separator? ~ "}" }
ascription  = { key ~ ":" ~ term }

identifier = @{ LETTER ~ (LETTER | ASCII_DIGIT | "_")* }

natural = { ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }

string               =  { double_quoted_string | single_quoted_string }
double_quoted_string = ${ "\"" ~ (!"\"" ~ char)* ~ "\"" }
single_quoted_string = ${ "\'" ~ (!"\'" ~ char)* ~ "\'" }

char = {
  
  | !"\\" ~ ANY
  | "\\" ~ ("\"" | "\'" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
